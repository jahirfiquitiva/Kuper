/*
 * Copyright (c) 2018. Jahir Fiquitiva
 *
 * Licensed under the CreativeCommons Attribution-ShareAlike
 * 4.0 International License. You may not use this file except in compliance
 * with the License. You may obtain a copy of the License at
 *
 *    http://creativecommons.org/licenses/by-sa/4.0/legalcode
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion versions.targetSdk
    buildToolsVersion versions.buildTools

    lintOptions {
        warningsAsErrors true
        disable 'MissingTranslation',
                'GoogleAppIndexingWarning',
                'UnusedResources',
                'ContentDescription',
                'RtlSymmetry',
                'RtlHardcoded',
                'RtlEnabled',
                'Overdraw'
    }

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.versionCode
        versionName versions.versionName
        vectorDrawables.useSupportLibrary = true
        proguardFiles 'proguard-android-optimize.txt', 'proguard-rules.pro'
        consumerProguardFiles 'proguard-android-optimize.txt', 'proguard-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles 'proguard-android-optimize.txt', 'proguard-rules.pro'
            consumerProguardFiles 'proguard-android-optimize.txt', 'proguard-rules.pro'
        }
    }

    aaptOptions {
        noCompress 'zw', 'zip', 'komp', 'klwp', 'kwgt', 'klck', 'kwch'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        pickFirst 'META-INF/core_debug.kotlin_module'
        pickFirst 'META-INF/core_release.kotlin_module'
        pickFirst 'META-INF/library_debug.kotlin_module'
        pickFirst 'META-INF/library_release.kotlin_module'
    }
}

dependencies {
    def exclusions = {
        exclude group: 'com.android.support'
        exclude module: 'appcompat-v7'
        exclude module: 'support-v4'
        exclude group: 'com.google.code'
        exclude group: 'org.jetbrains'
        exclude group: 'android.arch.core'
        exclude group: 'android.arch.lifecycle'
        exclude group: 'android.arch.room'
        exclude group: 'android.arch.persistence.room'
        exclude group: 'android.arch.persistence'
    }
    // Frames
    api(libraries.frames) {
        transitive = true
    }
    // Bottom Navigation
    api libraries.bottomNav, exclusions
    // Kustom API
    api libraries.kustom, exclusions
}

configurations.all {
    exclude module: 'constraint'
    exclude module: 'transition'
    exclude module: 'mediarouter-v7'
    exclude module: 'support-media-compat'
    exclude module: 'support-v4'
    exclude group: 'com.google.code'

    resolutionStrategy {
        force 'com.android.support:appcompat-v7:' + versions.supportLibs
        force 'com.android.support:support-v4:' + versions.supportLibs
        force 'android.arch.core:runtime:' + versions.arch
        force 'android.arch.core:common:' + versions.arch
        force 'android.arch.lifecycle:extensions:' + versions.arch
        force 'android.arch.lifecycle:compiler:' + versions.arch
        force 'android.arch.persistence.room:runtime:' + versions.arch
        force 'android.arch.persistence.room:compiler:' + versions.arch
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.contains("lint") || task.name.contains("test")) {
        task.enabled = false
    }
}

apply from: '../lib-artifacts.gradle'